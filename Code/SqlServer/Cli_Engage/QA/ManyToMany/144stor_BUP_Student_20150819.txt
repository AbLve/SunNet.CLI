ALTER PROCEDURE [dbo].[BUP_Student]
	@BUPStudentId int,
	@CreatedBy int
AS
BEGIN
	  SET XACT_ABORT ON;

	  DECLARE @Action tinyint
      DECLARE @CommunityName varchar(200)
      DECLARE @CommunityEngageID varchar(50)
      DECLARE @SchoolName varchar(200)
      DECLARE @SchoolEngageID varchar(50)
      DECLARE @FirstName varchar(140)
      DECLARE @MiddleName varchar(140)
      DECLARE @LastName varchar(140)
      DECLARE @StudentEngageId varchar(50)
      DECLARE @LocalStudentID varchar(50)
      DECLARE @BirthDate datetime
      DECLARE @Gender tinyint
      DECLARE @Ethnicity tinyint
      DECLARE @TSDSStudentID varchar(100)
      DECLARE @GradeLevel tinyint
	  DECLARE @ClassName VARCHAR(150)
      DECLARE @ClassEngageID varchar(32)
      DECLARE @ClassroomName varchar(50)
      DECLARE @ClassroomEngageID varchar(32)
	
	  DECLARE @SchoolId INT
	  DECLARE @CommunityId INT
	  DECLARE @StudentId INT --Students 表Id
	  DECLARE @ClassroomId INT --Classrooms 表Id
	  DECLARE @ClassId INT -- classes 表id

	BEGIN TRY
	  BEGIN TRANSACTION;

		 SELECT   @Action=[Action],@CommunityName=[CommunityName],@CommunityEngageID=[CommunityEngageID],@SchoolName=[SchoolName],@SchoolEngageID=[SchoolEngageID]
		  ,@FirstName=[FirstName],@MiddleName=[MiddleName],@LastName=[LastName],@StudentEngageId=StudentEngageId,@LocalStudentID=LocalStudentID
		  ,@BirthDate=BirthDate,@Gender=Gender,@Ethnicity=Ethnicity ,@TSDSStudentID=TSDSStudentID,@GradeLevel=GradeLevel
		  ,@ClassName=[ClassName],@ClassEngageID=[ClassEngageID],@ClassroomName=[ClassroomName]
		  ,@ClassroomEngageID=[ClassroomEngageID]
		  FROM [dbo].BUP_Students WHERE ID = @BUPStudentId

		  SELECT @SchoolId = ID FROM Schools WHERE SchoolId =@SchoolEngageID

		  SELECT @CommunityId =c.ID FROM Communities c
			INNER JOIN CommunitySchoolRelations r ON c.ID =r.CommunityId
			INNER JOIN Schools s ON s.ID = r.SchoolId
			WHERE s.ID=@SchoolId AND c.Status=1 AND c.CommunityId=@CommunityEngageID
		
		  IF(@ClassEngageID != '')
			SELECT @ClassId = id FROM Classes WHERE ClassId=@ClassEngageId AND SchoolId= @SchoolId

		  IF(@StudentEngageId != '')
			SELECT @StudentId = ID FROM Students WHERE StudentId=@StudentEngageId

	--INSERT
	IF(@Action = 1) BEGIN
		IF(@SchoolId IS NULL) BEGIN
			UPDATE BUP_Students SET Status=5 , Remark ='School Engage ID not in Schools' WHERE ID = @BUPStudentId
		END 
		ELSE BEGIN			
			IF(@CommunityId IS NULL)BEGIN
				UPDATE BUP_Students SET Status=5 , Remark ='Community Engage ID not in Communities' WHERE ID = @BUPStudentId
			END
			ELSE BEGIN
				INSERT INTO [dbo].[Students]
				([StudentId]
				,[FirstName]
				,[MiddleName]
				,[LastName]
				,[Status]
				,[StatusDate]
				,[SchoolYear]
				,[BirthDate]
				,[Gender]
				,[Ethnicity]
				,[EthnicityOther]
				,[PrimaryLanguageId]
				,[PrimaryLanguageOther]
				,[SecondaryLanguageId]
				,[SecondaryLanguageOther]
				,[IsSendParent]
				,[IsMediaRelease]
				,[Notes]
				,[CreatedOn]
				,[UpdatedOn]
				,[ParentCode]
				,[AssessmentLanguage]
				,[LocalStudentID]
				,[TSDSStudentID]
				,[GradeLevel])
				VALUES
				(''--<StudentId, varchar(12),>
				,@FirstName --, varchar(140),>
				,@MiddleName --, varchar(140),>
				,@LastName  --, varchar(140),>
				,1 --<Status, tinyint,>
				,getdate()--<StatusDate, datetime,>
				,dbo.GetSchoolYear()  --<SchoolYear, varchar(50),>
				,@BirthDate --, datetime,>
				,@Gender  --, tinyint,>
				,@Ethnicity --, tinyint,>
				,''--<EthnicityOther, varchar(100),>
				,0--<PrimaryLanguageId, int,>
				,''--<PrimaryLanguageOther, varchar(100),>
				,0--<SecondaryLanguageId, int,>
				,''--<SecondaryLanguageOther, varchar(100),>
				,0--<IsSendParent, bit,>
				,0--<IsMediaRelease, tinyint,>
				,''--<Notes, varchar(600),>
				,GETDATE() --<CreatedOn, datetime,>
				,GETDATE() --<UpdatedOn, datetime,>
				,''--<ParentCode, varchar(50),>
				,0 --<AssessmentLanguage, tinyint,>
				, @LocalStudentID --, varchar(100),>
				,@TSDSStudentID --, varchar(100),>
				,@GradeLevel --, tinyint,>
				)

				SELECT @StudentId = SCOPE_IDENTITY()

				INSERT INTO [dbo].[SchoolStudentRelations]
				([SchoolId]
				,[StudentId]
				,[Status]
				,[CreatedBy]
				,[CreatedOn]
				,[UpdatedOn]
				,[UpdatedBy])
				VALUES
				(@SchoolId
				,@StudentId
				,1
				,@CreatedBy
				,getdate()
				,getdate()
				,@CreatedBy)
				
				IF(@ClassEngageId != '')BEGIN					
					IF(@ClassId IS NULL) BEGIN
						UPDATE BUP_Students SET  Remark ='Class Engage Id not in the school' WHERE ID = @BUPStudentId
					END
					ELSE BEGIN						
						INSERT INTO [dbo].[StudentClassRelations]([StudentId],[ClassId])
						VALUES(@StudentId, @ClassId )
				    END
				END
			END
		END
	END  --END ACTION=1

	--UPDATE
	IF(@Action = 2) BEGIN
		IF(@SchoolId IS NULL) BEGIN
			UPDATE BUP_Students SET Status=5 , Remark ='School Engage ID not in Schools' WHERE ID = @BUPStudentId
		END
		ELSE BEGIN								
			IF(@CommunityId IS NULL)BEGIN
				UPDATE BUP_Students SET Status=5 , Remark ='Community Engage ID not in Communities' WHERE ID = @BUPStudentId
			END
			ELSE BEGIN
				IF(@StudentId IS NULL)
				   UPDATE BUP_Students SET Status=5 , Remark ='Student Engage ID not in Students' WHERE ID = @BUPStudentId
				ELSE BEGIN
					UPDATE [dbo].[Students]
					SET 
					 [FirstName] =  CASE WHEN @FirstName != '' THEN @FirstName ELSE FirstName END
					,[MiddleName] = CASE WHEN @MiddleName='*CLEAR*' THEN '' WHEN @MiddleName != '' THEN @MiddleName ELSE MiddleName END
					,[LastName] =  CASE WHEN @LastName != '' THEN @LastName ELSE LastName END
					,[BirthDate] =  CASE WHEN @BirthDate >'1753/1/1' THEN @BirthDate ELSE BirthDate END 
					,[Gender] =   CASE WHEN @Gender >0 THEN @Gender ELSE Gender END 
					,[Ethnicity] =CASE WHEN @Ethnicity = 127 THEN 0 WHEN @Ethnicity>0 THEN @Ethnicity ELSE Ethnicity END 
					,[UpdatedOn] = GETDATE()
					,[LocalStudentID] = CASE WHEN @LocalStudentID='*CLEAR*' THEN '' WHEN @LocalStudentID != '' THEN @LocalStudentID ELSE LocalStudentID END
					,[TSDSStudentID] =  CASE WHEN @TSDSStudentID='*CLEAR*' THEN '' WHEN @TSDSStudentID != '' THEN @TSDSStudentID ELSE TSDSStudentID END
					,[GradeLevel] = CASE WHEN @GradeLevel = 127 THEN 0 WHEN @GradeLevel>0 THEN @GradeLevel ELSE GradeLevel END 
					WHERE ID=@StudentId
				END
			END
			IF(@ClassEngageId != '')BEGIN
				IF(@ClassId IS NULL) BEGIN
					UPDATE BUP_Students SET  Remark ='Class Engage Id not in the school' WHERE ID = @BUPStudentId
				END
				ELSE BEGIN	
					IF(NOT EXISTS(SELECT * FROM StudentClassRelations WHERE StudentId=@StudentId AND ClassId=@ClassId))					
					INSERT INTO [dbo].[StudentClassRelations]([StudentId],[ClassId])
					VALUES(@StudentId, @ClassId )
				END
			END
		END

	END

	--DELETE
	IF(@Action = 3)BEGIN
		IF(@StudentId IS NULL)
			UPDATE BUP_Students SET Status=5 , Remark ='Student Engage ID not in Students' WHERE ID = @BUPStudentId
		ELSE BEGIN
			IF(@SchoolId IS NULL)
				UPDATE BUP_Students SET Status=5 , Remark ='School Engage ID not in Schools' WHERE ID = @BUPStudentId 
			ELSE
				BEGIN
					DELETE SchoolStudentRelations WHERE SchoolId=@SchoolId AND StudentId=@StudentId
					IF(@ClassEngageID != '')
					BEGIN
						IF(@ClassId IS NULL)
						  UPDATE BUP_Students SET  Remark ='Class Engage Id not in the school' WHERE ID = @BUPStudentId 
						ELSE
						  DELETE StudentClassRelations WHERE StudentId=@StudentId AND ClassId=@ClassId
					END
					IF(NOT EXISTS(SELECT * FROM SchoolStudentRelations WHERE StudentId=@StudentId) AND
					NOT EXISTS(SELECT * FROM StudentClassRelations WHERE StudentId=@StudentId))
					   UPDATE Students SET 
					   Status=2,StatusDate=GETDATE(),UpdatedOn=GETDATE() WHERE ID = @StudentId
				END
		END
	END

	UPDATE BUP_Students SET Status = 3 WHERE ID = @BUPStudentID AND Status = 1;
	  COMMIT TRANSACTION ;
	END TRY

	BEGIN CATCH
		IF (XACT_STATE()) = -1
		BEGIN			
			ROLLBACK TRANSACTION;
			UPDATE BUP_Students SET Remark = ERROR_MESSAGE() ,Status = 4 WHERE ID = @BUPStudentID
		END;

		IF (XACT_STATE()) = 1
		BEGIN
			COMMIT TRANSACTION;   
		END;
	END CATCH 
END